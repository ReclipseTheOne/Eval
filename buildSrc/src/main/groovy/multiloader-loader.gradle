plugins {
    id 'multiloader-common'
    id 'com.modrinth.minotaur'
    id 'net.darkhax.curseforgegradle'
}

// mc ver -> MAJOR.MINOR.x (not included for 1.20 since i gotta do diff releases)
def mc_ver_parts = minecraft_version.split('\\.')
def mc_file_version = "1.20.1"

configurations {
    commonJava{
        canBeResolved = true
    }
    commonResources{
        canBeResolved = true
    }
}

dependencies {
    commonJava project(path: ':common', configuration: 'commonJava')
    commonResources project(path: ':common', configuration: 'commonResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonJava)
    from(configurations.commonJava)
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = modrinth_project_id
    versionNumber = "${mc_file_version}-${project.name.capitalize()}"
    versionName = "${mod_name}-${version}-${mc_file_version}-(${project.name.capitalize()})"
    versionType = System.getenv('RELEASE_TYPE') ?: 'release'
    changelog = "Check GitHub for details"
    uploadFile = tasks.named('jar')
    gameVersions = [minecraft_version]
    loaders = [project.name]
    additionalFiles = [tasks.named('sourcesJar')]
    dependencies {
        if (project.name.equals('fabric')) {
            required.project "fabric-api"
        }
    }
    syncBodyFrom = rootProject.file('README.md').text
}

task curseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv('CURSEFORGE_TOKEN')

    def mainFile = upload(1405772, tasks.named('jar'))
    def sourcesFile = mainFile.withAdditionalFile(tasks.named('sourcesJar'))
    sourcesFile.displayName = "${mod_name}-${version}-${mc_file_version}-(${project.name.capitalize()})-sources"
    sourcesFile.changelog = "Check GitHub for details"

    mainFile.displayName = "${mod_name}-${version}-${mc_file_version}-(${project.name.capitalize()})"
    mainFile.releaseType = (System.getenv('RELEASE_TYPE') ?: 'release').toLowerCase()
    mainFile.addModLoader(project.name)
    mainFile.addGameVersion(minecraft_version)
    mainFile.changelog = "Check GitHub for details"

    if (project.name.equals('fabric')) {
        mainFile.addRequirement('fabric-api')
    }
}
